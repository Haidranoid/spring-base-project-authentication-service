plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
    id 'io.freefair.lombok' version '9.1.0'
    id 'jacoco'
}

group = 'com.spring-base-project'
version = '0.0.1'
description = 'authentication-service'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test

    classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/*Application*',
                        '**/common/**',
                        '**/configuration/**',
                ])
            })
    )

    reports {
        xml.required = true   // CI (Sonar, GitHub, etc.)
        html.required = true
        csv.required = false
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            element = 'CLASS'

            excludes = [
                    '*Application*',
                    '*common*',
                    '*configuration*',
                    '*controllers*',
            ]

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

repositories {
    mavenCentral()
    mavenLocal()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.9"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.1"
    }
}

dependencies {
    // Custom dependencies
    implementation 'com.spring-base-project:shared-starter:0.0.1'
    // Web dependencies
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework:spring-webflux'
    // Security dependencies
    implementation 'org.springframework.boot:spring-boot-starter-security'
    // Cloud config dependency
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    // Database dependencies
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql'
    // Docs dependencies
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'
    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'net.datafaker:datafaker:2.5.3'
}

tasks.named('test') {
    useJUnitPlatform()
    jvmArgs "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('mockito-core') }.absolutePath}"
}
